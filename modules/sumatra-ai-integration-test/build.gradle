/*
 * Copyright (c) 2009 - 2021, DHBW Mannheim - TIGERs Mannheim
 */

plugins {
    id 'sumatra.java'
}

sourceSets {
    integrationTest {
        java.srcDir "src/integrationTest/java"
    }
}

dependencies {
    integrationTestImplementation project(':common')
    integrationTestImplementation project(':common-math')
    integrationTestImplementation project(':common-bot')
    integrationTestImplementation project(':sumatra-simulation-control')
    integrationTestImplementation project(':sumatra-snapshot')
    integrationTestImplementation project(':sumatra-model')
    integrationTestImplementation project(':sumatra-skillsystem')
    integrationTestImplementation project(':moduli-ai')
    integrationTestImplementation project(':moduli-autoreferee')
    integrationTestImplementation project(':moduli-geometry')
    integrationTestImplementation project(':moduli-referee')
    integrationTestImplementation project(':moduli-wp')
    integrationTestImplementation project(':moduli-record')
    integrationTestImplementation project(':moduli-ai-controller')
    integrationTestImplementation project(':moduli-bot-params')
    integrationTestRuntimeOnly project(':moduli-botmanager-sim')

    integrationTestImplementation(libs.com.github.g3force.configurable)

    integrationTestImplementation(libs.org.apache.logging.log4j.log4j.api)
    integrationTestImplementation(libs.org.apache.logging.log4j.log4j.core)

    integrationTestImplementation(libs.junit)
    integrationTestRuntimeOnly(libs.org.junit.vintage.junit.vintage.engine)
    integrationTestImplementation(libs.org.assertj.assertj.core)

    integrationTestAnnotationProcessor(libs.org.projectlombok.lombok)
    integrationTestImplementation(libs.org.projectlombok.lombok)

    integrationTestImplementation(libs.org.openjdk.jmh.jmh.core)
    integrationTestAnnotationProcessor(libs.org.openjdk.jmh.jmh.generator.annprocess)
}

tasks.register('integrationTest', Test) {
    description = "Run the integration tests"
    group = "verification"

    maxHeapSize = "2g"

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    inputs.dir(file('config'))
}

tasks.register('runPathPlanningBenchmark', JavaExec) {
    group = "Execution"
    description = "Run PathPlanningPerfTest"
    classpath = sourceSets.integrationTest.runtimeClasspath
    mainClass.set("edu.tigers.sumatra.ai.integration.jmh.PathPlanningPerfTest")
    args("100")
}
